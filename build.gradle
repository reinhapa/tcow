plugins {
    id 'java'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.46.0'
    id 'net.nemerosa.versioning' version '3.0.0'
    id 'org.owasp.dependencycheck' version '8.2.1'
}

mainClassName = 'net.reini.tcow.TcowReport'

java {
  sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
//    mavenLocal()
    mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': 'net.reini.tcow.Billing'
    }
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute(module('com.lowagie:itext')).using(module('com.github.librepdf:openpdf:1.3.30'))
    }
}

dependencies {
    implementation 'bouncycastle:bcprov-jdk14:140'
    implementation 'ch.codeblock.qrinvoice.core:qrinvoice-core:1.17'
    implementation 'ch.codeblock.qrinvoice.openpdf:qrinvoice-openpdf:1.17'
    implementation 'com.itextpdf:forms:8.0.0'
    implementation 'com.itextpdf:kernel:8.0.0'
    implementation 'jakarta.mail:jakarta.mail-api:2.1.1'
    implementation 'net.sf.jasperreports:jasperreports-functions:6.20.5'
//    implementation 'net.sf.jasperreports:jasperreports-metadata:6.20.0'
    implementation 'net.sf.jasperreports:jasperreports:6.20.4'
    implementation 'org.odftoolkit:odfdom-java:0.11.0'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    runtimeOnly 'ch.qos.logback:logback-classic:1.4.7'
    runtimeOnly 'ch.qos.logback:logback-core:1.4.7'
    runtimeOnly 'org.eclipse.angus:jakarta.mail:2.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.9.3'
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
