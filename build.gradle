plugins {
    id 'java'
    id 'application'
    alias libs.plugins.ben.manes.versions
    alias libs.plugins.nemerosa.versioning
    alias libs.plugins.owasp.dependencycheck
}

java {
  sourceCompatibility = JavaVersion.VERSION_21
}

test {
  useJUnitPlatform()
}

repositories {
    mavenCentral()
    mavenLocal()
}

application {
    mainClass = 'net.reini.tcow.Billing'
}

dependencies {
    implementation libs.bouncycastle.bcprov
    implementation libs.qrinvoice.core
    implementation libs.qrinvoice.openpdf
    implementation libs.openpdf
    implementation libs.jakarta.mail.api
    implementation libs.jasper.reports
    implementation libs.jasper.reports.functions
    implementation libs.jasper.reports.metadata
    implementation libs.jasper.reports.jdt
    implementation libs.jasper.reports.pdf
    implementation libs.odftoolkit
    implementation libs.slf4j.api

    runtimeOnly libs.logback.classic
    runtimeOnly libs.logback.core
    runtimeOnly libs.angus.mail

    testImplementation libs.assertj.core
    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine
    testRuntimeOnly libs.junit.platform.runner
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
